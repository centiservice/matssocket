// MatsSocket-Client-Dart

// Hacky way to get Node.js here for "platform=node" tests
apply from: '../matssocket-client-javascript/node.gradle' // For the "node" extension

repositories {
    // Create a custom ivy repository that represents the dart sdk download, so that we
    // can download the SDK using gradles download mechanisms and cache.
    ivy {
        url = "https://storage.googleapis.com/dart-archive/channels/stable/release/"
        patternLayout {
            artifact "[revision]/sdk/[artifact]-[classifier]-release.[ext]"
        }
        metadataSources {
            artifact()
        }
    }
}

ext {
    // dartSdkVersion is set in root build.gradle (where also node is defined)
    dartDownloadTemp = file("$projectDir/dartsdk_download-temp")
    dartDownload = file("$projectDir/dartsdk_download")
    dartSdkPath = file("$dartDownload/dart-sdk")
}

configurations {
    dartSdk
}

// :: Find where Dart can be downloaded from, based on current OS (win, mac, linux)
def currentOs = org.gradle.internal.os.OperatingSystem.current()
dependencies {
    def platformClassifier =
            currentOs.isWindows() ? "windows-x64" :
            currentOs.isMacOsX() ? "macos-x64" :
            currentOs.isLinux() ? "linux-x64" : null
    if (platformClassifier == null) {
        throw new GradleException("Unsupported OS for Dart SDK: ${currentOs}")
    }
    dartSdk "com.google.dart:dartsdk:${dartSdkVersion}:${platformClassifier}@zip"
}

tasks.register('build') {
    dependsOn archiveLib, test, dartDoc
}

tasks.register('download') {
    dependsOn nodeBinDir, dartBinPath
}

tasks.register('downloadAll') {
    dependsOn download, dartPubGet
}

// ===============================
// Dart tasks

// :: Small helper to run the Dart executable with common configuration.
def dartExec(Closure body) {
    services.get(ExecOperations).exec { ExecSpec spec ->
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        spec.executable currentOs.getExecutableName("$dartSdkPath/bin/dart")
        spec.environment 'PATH', "$dartSdkPath/bin${File.pathSeparator}${System.getenv('PATH')}" // for 'dart pana'
        spec.workingDir projectDir
        body.delegate = spec
        body.resolveStrategy = Closure.DELEGATE_FIRST
        body()
    }
}
// .. same, if only args
def dartExec(Object... cmd) {
    dartExec {
        args cmd
    }
}

tasks.register('installDart') {
    // ---- Declare inputs ----
    inputs.files(configurations.dartSdk)        // The Dart SDK zip file

    // ---- Declare outputs ----
    outputs.dir(dartDownload)                   // unpacked SDK

    doLast {
        println "Pre-cleaning Dart temp dir '$dartDownloadTemp' and target dir '$dartDownload'"
        delete "$dartDownloadTemp"
        delete "$dartDownload"
        // Fetch the Dart SDK from "custom ivy repo" set up above
        println "Unzipping Dart SDK to temp dir '$dartDownloadTemp'"
        copy {
            from zipTree(configurations.dartSdk.singleFile)
            into "$dartDownloadTemp"
        }

        // Move it into place - avoids problem if IntelliJ is set up to use the Dart SDK here ("Text file busy")
        // Note: If on Windows, you'll probably have to find another SDK location for IntelliJ, as it locks the files.
        println "Renaming Dart SDK download to '$dartDownload'"
        dartDownloadTemp.renameTo(dartDownload)

        println "All good, cleaning Dart temp dir '$dartDownloadTemp'"
        delete "$dartDownloadTemp"
    }
}

tasks.register('dartPubGet') {
    dependsOn 'installDart'

    // ---- Declare inputs ----
    inputs.files(configurations.dartSdk)        // The Dart SDK zip file
    inputs.file("pubspec.yaml")                 // Specification of dependencies

    // ---- Declare outputs ----
    outputs.dir(dartDownload)                   // unpacked SDK
    outputs.dir("${projectDir}/.dart_tool")     // dart pub get stuff
    outputs.file("pubspec.lock")                // the resulting pubspec lock file

    doLast {
        // Update dart dependencies by executing 'pub get'
        println "Executing 'dart pub get' to resolve and fetch dependencies."
        dartExec 'pub', 'get'
        dartExec '--version' // Just to show which dart version we are using.
    }
}

tasks.register('dartBinPath') {
    dependsOn installDart
    doLast {
        println "=== Dart executable: ${currentOs.getExecutableName("$dartSdkPath/bin/dart")}"
        println "=== Dart bin dir   : $dartSdkPath/bin/"
        println "    Unix:     export PATH=$dartSdkPath/bin/:\$PATH"
        println "    Windows:  set PATH=$dartSdkPath/bin/;%PATH%"
    }
}

// :: 'dart doc'
tasks.register('dartDoc') {
    dependsOn dartPubGet

    // Inputs: source files and pubspec that affect docs
    inputs.dir("$projectDir/lib")
    inputs.file("$projectDir/pubspec.yaml")
    inputs.file("$projectDir/README.md").optional(true)
    inputs.file("$projectDir/CHANGELOG.md").optional(true)
    inputs.file("$projectDir/analysis_options.yaml").optional(true)

    // Outputs: doc/api directory
    outputs.dir("$projectDir/doc")

    doFirst {
        delete "$projectDir/doc"
    }
    doLast {
        dartExec 'doc'
    }
}


// :: 'dart outdated'
tasks.register('dartOutdated') {
    dependsOn dartPubGet
    doLast {
        dartExec 'pub', 'outdated'
    }
}

// :: 'dart upgrade'
tasks.register('dartUpgradeUpdate') {
    dependsOn dartPubGet
    doLast {
        dartExec 'pub', 'upgrade', '--major-versions'
    }
}

// :: 'dart version'
tasks.register('dartVersion') {
    dependsOn dartPubGet
    doLast {
        dartExec '--version'
    }
}
tasks.register('versions') { dependsOn dartVersion }

// :: 'dart pub global run pana' - to check whether we get full score on pub.dev
tasks.register('dartPana') {
    dependsOn dartPubGet
    doLast {
        dartExec 'pub', 'global', 'run', 'pana'
    }
}

// 'dart publish --dry-run' // == NOTE! DO A './gradlew -PchromePath=<path> testAll' first!
tasks.register('dartPublishDryRun') {
    dependsOn dartPubGet, testVmKernel, dartDoc
    doLast {
        dartExec 'pub', 'publish', '--dry-run'
    }
}

// 'dart publish' // == NOTE! DO A './gradlew -PchromePath=<path> testAll' first!
// Check versions in pubspec.yaml and MatsSocket.dart
tasks.register('dartPublish') {
    dependsOn dartPubGet, testVmKernel, dartDoc
    doLast {
        dartExec 'pub', 'publish'
    }
}

// Package up the Dart library
tasks.register('archiveLib', Zip) {
    from "$projectDir/lib"
    into "mats_socket-${project.version}-dart"
    def destinationDir = layout.buildDirectory.dir("dist").get().asFile
    def archiveName = "matssocket-${project.version}-dart.zip"
    destinationDirectory = destinationDir
    archiveFileName = archiveName
    doLast {
        println("##### Dart Client zip: " + destinationDir + "/" + archiveName)
    }
}

// ===============================
// Comprehensive test setup!

// :: This is an experiment of running dart from "command line". Setting path for node, and then invoking dart.
tasks.register('experimentWithExecutingDartAndNode', Exec) {
    dependsOn nodeBinDir
    doFirst {
        println "NODE_BIN_DIR (from project.ext): $NODE_BIN_DIR"
        environment 'PATH', "${NODE_BIN_DIR}${File.pathSeparator}$dartSdkPath/bin${File.pathSeparator}${System.getenv('PATH')}"
        // For some frikkin' reason, the above-set PATH will not affect the lookup of the executable (i.e. just "dart"),
        // so we need set executable with absolute path.
        executable currentOs.getExecutableName("$dartSdkPath/bin/dart")
        // ALSO, globs like "test/*.dart' doesn't expand - that's a shell's job. So we need to do it ourselves.
        // A trick is to actually run 'sh -c <command>' instead, but that's not portable.
        def testFiles = file("$projectDir/test")
                .listFiles().findAll { it.name.endsWith(".dart") }.collect { "test/${it.name}" }
        args = ['test', '-p', 'node', '-c', 'dart2js', '--reporter=expanded'] + testFiles
    }
}

// Helper to register a Dart test task with platform and compiler variants.
def registerDartTestTask = { String name, String platform, String compiler ->
    tasks.register(name) {
        dependsOn 'dartPubGet', ':startMatsSocketTestServer'
        if (platform == 'node') {
            dependsOn 'nodeBinDir' // Need to have figured out where node is.
        }

        def chromePath = providers.gradleProperty('chromePath')
        def dartLogLevel = providers.gradleProperty('dartLogLevel')

        doLast {
            dartExec {
                // :: Setting environment for test run
                var matsSocketUrls = rootProject.wsUrls.join('$')
                environment("MATS_SOCKET_URLS", matsSocketUrls)

                var logLevel
                if (dartLogLevel.isPresent()) {
                    logLevel = dartLogLevel.get()
                } else {
                    // Checking which log level is enabled to pass on to the tests, so that they log if needed.
                    if (logger.isEnabled(LogLevel.DEBUG)) {
                        logLevel = "ALL"
                    } else if (logger.isEnabled(LogLevel.INFO)) {
                        logLevel = "INFO"
                    } else {
                        logLevel = "SEVERE"
                    }
                }
                environment("LOG_LEVEL", logLevel)

                def testFiles = file("$projectDir/test")
                        .listFiles().findAll { it.name.endsWith(".dart") }.collect { "test/${it.name}" }
                // We want to set LOG_LEVEL and MATS_SOCKET_URLS using compile constants, using 'define'.
                // BUT: The define stuff doesn't work as of 2025-09-21: https://github.com/dart-lang/sdk/issues/51791
                // Had it worked, it should be picked up in "test/lib/env.dart"
                args = ["-DLOG_LEVEL=${logLevel}", "-DMATS_SOCKET_URLS=\"${matsSocketUrls}\"",
                        'test', '-p', platform, '-c', compiler, '--reporter=expanded', '--concurrency', '1'] + testFiles
                if (platform == 'chrome' && chromePath.isPresent()) {
                    println "Setting env CHROME_EXECUTABLE=${chromePath.get()}"
                    environment("CHROME_EXECUTABLE", chromePath.get())
                }
                if (platform == 'node') {
                    println "Adding Node to PATH: ${NODE_BIN_DIR}"
                    environment 'PATH', "${NODE_BIN_DIR}${File.pathSeparator}${System.getenv('PATH')}"
                }
                println "Executing 'dart ${args.join(" ")}' to run tests."
            }
        }
    }
}

// :: Define the various test tasks with their platform/compiler variants.
registerDartTestTask('testVmExe', 'vm', 'exe')
registerDartTestTask('testVmSource', 'vm', 'source')
registerDartTestTask('testVmKernel', 'vm', 'kernel')
registerDartTestTask('testNodeJs', 'node', 'dart2js')
registerDartTestTask('testNodeWasm', 'node', 'dart2wasm')
registerDartTestTask('testWebJs', 'chrome', 'dart2js')     // Might want to set -PchromePath=
registerDartTestTask('testWebWasm', 'chrome', 'dart2wasm') // Might want to set -PchromePath=

// .. Subsets
tasks.register('testVm') { dependsOn 'testVmExe', 'testVmSource', 'testVmKernel' }
tasks.register('testNode') { dependsOn 'testNodeJs', 'testNodeWasm'}
tasks.register('testWeb') { dependsOn 'testWebJs', 'testWebWasm' }  // Might want to set -PchromePath=

// .. All tests
tasks.register('testAll') { dependsOn 'testVm', 'testNode', 'testWeb' }   // Might want to set -PchromePath=

// .. Make 'test' be both VM and Node - but handle bug with Node+WASM on Windows.
// ?: Are we running on Windows? (GHA)
if (currentOs.isWindows()) {
    // -> Yes, Windows, special handling. Avoid the testNodeWasm, as it fails due to bad path handling,
    // e.g. '/runner' becomes '<linefeed>unner', and fails.
    tasks.register('test') { dependsOn 'testVm', 'testNodeJs' }
}
else {
    // -> No, not Windows, so include all testVm and testNode tasks.
    tasks.register('test') { dependsOn 'testVm', 'testNodeJs', 'testNodeWasm' }
}

// We need to ensure that dart tests are run before stopping the test MatsSocketServer.
rootProject.stopMatsSocketTestServer.mustRunAfter(testWebJs, testWebWasm, testVmExe, testVmSource, testVmKernel,
        testNodeJs, testNodeWasm, test, testAll)

// ===== Clean tasks

tasks.register('clean', Delete) {
    delete layout.buildDirectory
    delete "$projectDir/doc"
}

tasks.register('distclean', Delete) {
    dependsOn clean, distcleanNode
    delete "$dartDownload"
    delete "$dartDownloadTemp"
    delete "$projectDir/.dart_tool"
    delete "$projectDir/pubspec.lock"
    delete "$projectDir/package-lock.json" // If npmUpdate is run in this submodule
}
