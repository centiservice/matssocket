// MatsSocket-Client-Dart

repositories {
    // Create a custom ivy repository that represents the dart sdk download, so that we
    // can download the SDK using gradles download mechanisms and cache.
    ivy {
        url "https://storage.googleapis.com/dart-archive/channels/stable/release/"
        patternLayout {
            artifact "[revision]/sdk/[artifact]-[classifier]-release.[ext]"
        }
        metadataSources {
            artifact()
        }
    }
}

configurations {
    dartSdk
}


ext {
    dartSdkVersion = '3.8.1'
    dartDownloadTemp = file("$projectDir/dartsdk_download-temp")
    dartDownload = file("$projectDir/dartsdk_download")
    dartSdkPath = file("$dartDownload/dart-sdk")
}

dependencies {
    def os = org.gradle.internal.os.OperatingSystem.current();
    if (os.isWindows()) {
        dartSdk "com.google.dart:dartsdk:${dartSdkVersion}:windows-x64@zip"
    }
    else if (os.isMacOsX()) {
        dartSdk "com.google.dart:dartsdk:${dartSdkVersion}:macos-x64@zip"
    }
    else if (os.isLinux()) {
        dartSdk "com.google.dart:dartsdk:${dartSdkVersion}:linux-x64@zip"
    }
}

// Small helper to run the Dart executable with common configuration.
def dartExec(Closure body) {
    exec {
        def os = org.gradle.internal.os.OperatingSystem.current()
        executable os.getScriptName("$dartSdkPath/bin/dart")
        workingDir projectDir
        body.delegate = delegate
        body.resolveStrategy = Closure.DELEGATE_FIRST
        body()
    }
}
def dartExec(Object... cmd) {
    dartExec {
        args cmd
    }
}

task updateDartDependencies() {
    doFirst {
        println "Cleaning DART temp dir '$dartDownloadTemp'"
        delete dartDownloadTemp
    }
    doLast {
        // Fetch the Dart SDK from "custom ivy repo" set up above
        println "Unzipping DART SDK to '$dartDownloadTemp'"
        copy {
            from zipTree(configurations.dartSdk.asPath)
            into "$dartDownloadTemp"
        }

        // Move it into place - avoids problem if IntelliJ is set up to use the Dart SDK here ("Text file busy")
        // Note: If on Windows, you'll probably have to find another SDK location for IntelliJ, as it locks the files.
        println "Renaming DART SDK download to '$dartDownload'"
        dartDownloadTemp.renameTo(dartDownload)

        // Update dart dependencies by executing 'pub get'
        dartExec 'pub', 'get'
    }
}

task test(dependsOn: ['updateDartDependencies', ':startMatsSocketTestServer']) {
    doLast {
        dartExec {
            // :: Setting environment for test run
            environment("MATS_SOCKET_URLS", rootProject.startMatsSocketTestServer.wsUrls.join(","))
            // Checking which log level is enabled to pass on to the tests, so that they log if needed.
            if (logger.isEnabled(LogLevel.DEBUG)) {
                environment("LOG_LEVEL", "DEBUG")
            }
            else if (logger.isEnabled(LogLevel.INFO)) {
                environment("LOG_LEVEL", "INFO")
            }
            else {
                environment("LOG_LEVEL", "SEVERE")
            }
            def platform = "vm"
            if (project.hasProperty('platform')) {
                platform = project.getProperties()['platform']
            }
            def testFiles = file("$projectDir/test").listFiles().findAll { it.name.endsWith(".dart") }
            args = ['run', 'test'] + testFiles + ['-p', platform, '--reporter=expanded', '-j', '1']
        }
    }
}
// We need to ensure that dart tests are run before stopping the test MatsSocketServer.
rootProject.stopMatsSocketTestServer.mustRunAfter(test)

// 'dart publish --dry-run'
task publishDryRun(dependsOn:  updateDartDependencies) {
    doLast {
        dartExec 'publish', '--dry-run'
    }
}

// 'dart publish --force'
task publish(dependsOn:  updateDartDependencies) {
    doLast {
        dartExec 'publish', '--force'
    }
}

// Package up the Dart library
task archiveLib(type: Zip) {
    from "$projectDir/lib"
    into "mats_socket-${project.version}-dart"
    archiveFileName = "mats_socket-${project.version}-dart.zip"
    destinationDirectory.set(layout.buildDirectory.dir("dist"))
}

// Include the packaging of the lib into the main build
rootProject.build.dependsOn(archiveLib)

tasks.register('clean', Delete) {
    delete layout.buildDirectory
}

// "distclean": Clean out the downloaded Dart SDK and dependencies
task distclean(type: Delete, dependsOn: [clean]) {
    delete "$dartDownload"
    delete "$projectDir/.dart_tool"
    delete "$projectDir/.packages" // Old-style .dart_tool/package_config.json
    delete "$projectDir/pubspec.lock"
    doLast {
        println "NOTICE: Not deleting '~/.pub-cache'"
    }
}
