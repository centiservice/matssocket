// MatsSocket-Client-JavaScript TESTS

apply from: '../node.gradle'

tasks.register('build') {dependsOn test }

// Execute only the unit tests
tasks.register('testUnit', NpmTask) {
    dependsOn npmInstall, ':matssocket-client-javascript:client:build'
    args = ['testUnit']
}

// Execute only the basic tests
tasks.register('testBasic', NpmTask) {
    dependsOn npmInstall, ':startMatsSocketTestServer'
    args = ['testBasic']
    doFirst {
        execOverrides {
            it.environment("MATS_SOCKET_URLS", rootProject.wsUrls.join(","))
        }
    }
}

// Execute both unit and integration tests against a test MatsSocketTestServer
tasks.register('test', NpmTask) {
    dependsOn npmInstall, ':startMatsSocketTestServer'
    args = ['test']
    doFirst {
        execOverrides {
            it.environment("MATS_SOCKET_URLS", rootProject.wsUrls.join(","))
        }
    }
}

// Run the integration test continuously, retriggering on change in the javascript code. This assumes there is
// a server listening on ws://localhost:8080/matssocket
tasks.register('javascriptTestWatch', NpmTask) {
    dependsOn npmInstall, ':startMatsSocketTestServer'
    args = ['test', '--watch']
    doFirst {
        execOverrides {
            it.environment("MATS_SOCKET_URLS", rootProject.wsUrls.join(","))
        }
    }
}

// We need to ensure that javascript tests are run before MatsSocketTestServer stops!
rootProject.stopMatsSocketTestServer.mustRunAfter(test)
rootProject.stopMatsSocketTestServer.mustRunAfter(testBasic)

tasks.register('clean', Delete) {
    delete layout.buildDirectory
    delete "$projectDir/bundles/"
}

tasks.register('distclean', Delete) {dependsOn distcleanNode }
