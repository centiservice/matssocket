// MatsSocket-Client-JavaScript TESTS

apply from: '../node.gradle'


tasks.register('build', YarnTask) {
    dependsOn ':matssocket-client-javascript:client:build', 'yarn'
    args = ['build']
    inputs.files('package.json', 'yarn.lock', 'rollup.config.js')
    inputs.dir('../tests_esm/src')
    outputs.dir('bundles')
}

// Execute both unit and integration tests against a test MatsSocketTestServer
tasks.register('test', YarnTask) {
    dependsOn 'cleanNodeModules', 'yarn', 'build', ':startMatsSocketTestServer'
    args = ['test']
    doFirst {
        execOverrides {
            it.environment("MATS_SOCKET_URLS", rootProject.wsUrls.join(","))
        }
    }
}

// We need to ensure that javascript tests are run before MatsSocketTestServer stops!
rootProject.stopMatsSocketTestServer.mustRunAfter(test)

tasks.register('cleanNodeModules', Delete) {
    delete "$projectDir/node_modules/"
}

tasks.register('clean', Delete) {
    delete layout.buildDirectory
    delete "$projectDir/bundles/"
}

// Clean out the downloaded node & yarn
tasks.register('distclean', Delete) {
    dependsOn clean, cleanNodeModules
    delete "$projectDir/node_download/"
}
