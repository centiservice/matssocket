// MatsSocket-Client-JavaScript TESTS

apply from: '../node.gradle'

// Avoid circular dependency
tasks.register('build') { dependsOn build_internal, test }

tasks.register('build_internal', NpmTask) {
    dependsOn npmInstall, ':matssocket-client-javascript:client:build'
    args = ['run', 'build']
    inputs.files('package.json', 'package-lock.json', 'rollup.config.js')
    inputs.dir('../tests_esm/src')
    outputs.dir('dist')
}

// Execute both unit and integration tests against a test MatsSocketTestServer
tasks.register('test', NpmTask) {
    dependsOn npmInstall, 'build_internal', ':startMatsSocketTestServer'
    args = ['test']
    doFirst {
        execOverrides {
            it.environment("MATS_SOCKET_URLS", rootProject.wsUrls.join(","))
        }
    }
}

tasks.register('download') {
    dependsOn nodeBinDir
}

tasks.register('downloadAll') {
    dependsOn download, npmUpdate
}

// We need to ensure that javascript tests are run before MatsSocketTestServer stops!
rootProject.stopMatsSocketTestServer.mustRunAfter(test)

tasks.register('clean', Delete) {
    delete layout.buildDirectory
    delete "$projectDir/dist/"
}

tasks.register('distclean', Delete) {dependsOn clean, distcleanNode }
